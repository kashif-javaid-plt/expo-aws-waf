apply plugin: 'com.android.library'

// Apply maven-publish plugin only for JitPack builds
def isJitPackBuild = System.getenv("JITPACK") == "true"
if (isJitPackBuild) {
  apply plugin: 'maven-publish'
}

group = 'expo.modules.awswaf'
version = '0.1.0'

def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
apply from: expoModulesCorePlugin
applyKotlinExpoModulesCorePlugin()
useCoreDependencies()

// Use Expo publishing for local development, JitPack publishing for distribution
if (!isJitPackBuild) {
  useExpoPublishing()
}

// If you want to use the managed Android SDK versions from expo-modules-core, set this to true.
// The Android SDK versions will be bumped from time to time in SDK releases and may introduce breaking changes in your module code.
// Most of the time, you may like to manage the Android SDK versions yourself.
def useManagedAndroidSdkVersions = false
if (useManagedAndroidSdkVersions) {
  useDefaultAndroidSdkVersions()
} else {
  buildscript {
    // Simple helper that allows the root project to override versions declared by this library.
    ext.safeExtGet = { prop, fallback ->
      rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }
  }
  project.android {
    compileSdkVersion safeExtGet("compileSdkVersion", 36)
    defaultConfig {
      minSdkVersion safeExtGet("minSdkVersion", 24)
      targetSdkVersion safeExtGet("targetSdkVersion", 36)
    }
  }
}

android {
  namespace "expo.modules.awswaf"
  defaultConfig {
    versionCode 1
    versionName "0.1.0"
  }
  lintOptions {
    abortOnError false
  }
}

dependencies {
  if (isJitPackBuild) {
    // For JitPack builds, use compileOnly to avoid the AAR conflict
    compileOnly files('libs/WafMobileSdk.aar')
  } else {
    // For local development, use implementation
    implementation files('libs/WafMobileSdk.aar')
  }
  
  // Required dependencies for AWS WAF Android Mobile SDK
  implementation 'com.google.code.gson:gson:2.8.9'
  implementation 'org.bouncycastle:bcprov-jdk15to18:1.80'
}

// Publishing configuration for JitPack (only when building for JitPack)
if (isJitPackBuild) {
  afterEvaluate {
    publishing {
      publications {
        release(MavenPublication) {
          from components.release
          groupId = 'com.github.kashif-javaid-plt'
          artifactId = 'expo-aws-waf'
          version = '1.0.0'
          
          // Include the WAF SDK .aar as an additional artifact
          artifact(file('libs/WafMobileSdk.aar')) {
            classifier = 'waf-sdk'
            extension = 'aar'
          }
        }
      }
    }
  }
}
